import org.snakeskin.compiler.units.GenerateUnitsClasses

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id "com.palantir.git-version" version "0.12.2"
    id "com.jfrog.bintray" version "1.8.4"
}

group 'org.snakeskin'
version gitVersion()

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/generated'
}

task genUnitsClasses(type: GenerateUnitsClasses)

compileKotlin.dependsOn genUnitsClasses

publishing {
    publications {
        Units(MavenPublication) {
            from components.java
            groupId 'org.snakeskin'
            artifactId 'SnakeSkin-Units'
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    publications = ['Units']
    pkg {
        repo = "SnakeSkin"
        name = "SnakeSkin-Units"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin-units"
        version {
            name = this.version
        }
    }
}

wrapper {
    gradleVersion = '6.0.1'
}